config {
  type: "incremental",
  name: "taxi",
  description: "Taxi incremental table",
  columns: docs.taxi_columns,
  bigquery: {
    partitionBy: "TIMESTAMP_TRUNC(trip_start_timestamp, MONTH)",
    clusterBy: ["payment_type"]
  },
  assertions: {
    nonNull: ["unique_key", "taxi_id"]
  },
  tags: ["daily"]

}

pre_operations {
  ${when(incremental(),`DECLARE max_trip_start_time DEFAULT (
    SELECT max(trip_start_timestamp) FROM ${self()}
  );`)}
}


Select * from ${constants.sourceTable}
where  trip_miles > 0.5 and trip_seconds > 30 
and safe_divide(trip_miles,trip_seconds) < 0.0346 
and safe_divide(trip_miles,trip_seconds) > 0.000172603

${when(incremental(),` and trip_start_timestamp > max_trip_start_time 
and trip_start_timestamp < cast(date_add(cast(timestamp_trunc(max_trip_start_time, MONTH) as date), INTERVAL 2 MONTH) as timestamp)`)}
