config {
  type: "incremental",
  name: "taxi",
  description: "Taxi incremental table",
  columns: docs.taxi_columns,
  bigquery: {
    partitionBy: "TIMESTAMP_TRUNC(trip_start_timestamp, DAY)",
    clusterBy: ["payment_type"]
  },
  assertions: {
    nonNull: ["unique_key", "taxi_id"]
  },
  tags: ["daily"]

}

pre_operations {
  ${when(incremental(),`DECLARE max_trip_start_time DEFAULT (
    SELECT max(trip_start_timestamp) FROM ${self()}
  );`)}
}


Select * from ${constants.sourceTable}
where trip_seconds > 30 and trip_miles > 0.5 and trip_miles/trip_seconds < 0.0346 and trip_miles/trip_seconds > 0.000172603

${when(incremental(),` and timestamp_trunc(trip_start_timestamp, DAY) > max_trip_start_time and timestamp_trunc(trip_start_timestamp, DAY) < timestamp(date_add(max_trip_start_time, interval 3 day))`)}
